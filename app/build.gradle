import fr.xgouchet.gradle.AndroidBuild
import fr.xgouchet.gradle.Dependencies

plugins {
    id 'com.github.ben-manes.versions'
    id "io.gitlab.arturbosch.detekt"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {

    compileSdkVersion AndroidBuild.TargetSdk
    buildToolsVersion AndroidBuild.BuildTools

    defaultConfig {

        minSdkVersion AndroidBuild.MinSdk
        targetSdkVersion AndroidBuild.TargetSdk

        applicationId "fr.xgouchet.rehearsal"
        versionCode 1
        versionName "0.1"

        multiDexEnabled true


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
        }
    }

    // Used for testing Room migration
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    compile project(":fountain-parser")

    implementation Dependencies.Libraries.Kotlin
    implementation Dependencies.Libraries.MultiDex

    implementation Dependencies.Libraries.AndroidX
    implementation Dependencies.Libraries.MaterialComponents
    implementation Dependencies.Libraries.AndroidArchComponents
    implementation Dependencies.Libraries.AndroidConstraintLayout
    implementation Dependencies.Libraries.Rx

    implementation Dependencies.Libraries.ArchX
    implementation Dependencies.Libraries.Spectrum

    api Dependencies.Libraries.Markdown
    implementation Dependencies.Libraries.AboutPage
    implementation Dependencies.Libraries.Timber
    implementation Dependencies.Libraries.Stetho

    kapt Dependencies.Processors.AnrdoidXArch

    testImplementation Dependencies.Libraries.Testing
    androidTestImplementation Dependencies.Libraries.Testing
    androidTestImplementation Dependencies.Libraries.AndroidTesting
}

configurations {
    compile.exclude group: 'com.android.support'
}
